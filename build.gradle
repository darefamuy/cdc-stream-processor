plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'jacoco'
}

group   = 'com.abbank'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

application {
    mainClass = 'com.abbank.streams.ABBankStreamsApp'
}

repositories {
    mavenCentral()
    maven { url = 'https://packages.confluent.io/maven/' }
}

ext {
    kafkaVersion      = '4.1.1'
    confluentVersion  = '8.1.1'
    slf4jVersion      = '2.0.12'
    logbackVersion    = '1.5.3'
    jacksonVersion    = '2.17.0'
    junitVersion      = '5.10.2'
    mockitoVersion    = '5.11.0'
    assertjVersion    = '3.25.3'
}

dependencies {
    implementation "org.apache.kafka:kafka-streams:${kafkaVersion}"
    implementation "org.apache.kafka:kafka-clients:${kafkaVersion}"
    implementation "io.confluent:kafka-streams-avro-serde:${confluentVersion}"
    implementation "io.confluent:kafka-schema-registry-client:${confluentVersion}"
    implementation "io.confluent:kafka-avro-serializer:${confluentVersion}"
    implementation 'org.apache.avro:avro:1.11.3'
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation 'com.typesafe:config:1.4.3'

    testImplementation "org.apache.kafka:kafka-streams-test-utils:${kafkaVersion}"
    testImplementation "io.confluent:kafka-schema-registry-client:${confluentVersion}"
    testImplementation "io.confluent:kafka-streams-avro-serde:${confluentVersion}"
    testImplementation 'org.apache.avro:avro:1.11.3'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly    "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "ch.qos.logback:logback-classic:${logbackVersion}"
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
        showExceptions = true
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required  = true
        html.required = true
    }
}

shadowJar {
    archiveClassifier = ''
    mergeServiceFiles()
    manifest { attributes 'Main-Class': application.mainClass }
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

tasks.distZip.dependsOn(shadowJar)
tasks.distTar.dependsOn(shadowJar)
tasks.startScripts.dependsOn(shadowJar)
tasks.startShadowScripts.dependsOn(jar)

build.dependsOn shadowJar
